/*------------------------
--   CLIENT-JAVA-12
--------------------------*/

	/**
	  THIS script is refer to the exact same source code of eQuiz-Client
		and compile it using Java-12 or any version later
		see more in README.md

		$ gradle run -Dorg.gradle.java.home="C:\Program Files\Java\jdk-12.0.2"
	*/	
	plugins {
		id 'edu.sc.seis.launch4j' version '2.4.6' //https://github.com/TheBoegl/gradle-launch4j
		id 'java'
		id 'application' //to use 'gradle run'
		
		id 'org.openjfx.javafxplugin' version '0.0.7'
		
		// Create a custom runtime image
		// https://badass-runtime-plugin.beryx.org/releases/latest/
		id 'org.beryx.runtime' version '1.2.1' //Badass 
	}

	//--- IMPORTANT CHANGES ----
		buildDir = "D:/build/equiz-client-java12"

		mainClassName = 'frawla.equiz.client.Launcher'
		def splashImage = 'splash.jpg'
		def projPath = "../../eqClient"	
		def jreImageDir = "equiz-client-with-jre" //JRE image ($buildDir/image) eqClient-linux-x64
		def verdetls = versionDetails()
		gitLastTag = verdetls.lastTag
		
	//---------------------------------
	
	applicationDefaultJvmArgs = ["-splash:$splashImage"]
	javafx {
		version = "11.0.2" //version 11 hav long support.
		modules = [ 'javafx.base', 'javafx.controls', 'javafx.fxml',
					'javafx.swing', 'javafx.graphics', 'javafx.web' ]     
		//'javafx.graphics',
	}

	sourceSets {
		main {
			java { srcDirs = ["$projPath/src/main/java"] }
			resources { srcDirs = ["$projPath/src/main/resources"] }
		}
	}
	
	dependencies
	{
		implementation project (":eqCore")
		
		testImplementation 'org.junit.jupiter:junit-jupiter:5.5.0'
		
		//JSAP (Java Simple Arguments Parser)
		implementation 'com.martiansoftware:jsap:2.1'
	}
	
	jar {
		archiveVersion = '' //to avoid write it in Jar file name
    	manifest {
    	  attributes( 'Class-Path': ".",
    	 			'Implementation-Title': "eQuiz Manifest",
            'Implementation-Version': gitLastTag, 
            'Main-Class': mainClassName
				)
    	}
	}
	

	
	runtime 
	{
		imageDir = file("$buildDir/$jreImageDir")  //JRE image

	  //jlink options
	  options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
		
		//targetPlatform 'j11-win64', 'D:/Program Files/java/jdk-11.0.6_windows-x64',['--endian', 'big']
		//targetPlatform 'linux64', 'D:/Program Files/java/jdk-13.0.2_linux-x64'
		//targetPlatform 'j13-win64', 'D:/Program Files/java/jdk-13.0.2_windows-x64'

		//targetPlatform 'win64', 'C:/Program Files/Java/jdk-12.0.2' // --> OK
		//targetPlatform 'win64', 'D:/Program Files/java/jdk-12.0.2_windows-x64' // --> OK

	}

	task runtimeClean(dependsOn: 'runtime'){
		mustRunAfter 'runtime'
		doLast {
			delete{
				delete "$buildDir/jre"
				delete "$buildDir/libs"
				delete "$buildDir/install"
			}		
			println 'image is created !!!'
		}
	}


	apply from: file('../../eqCore/about.gradle')
	makeAboutImage {
		def res = "$projPath/src/main/resources/images"
		inputFile = file("$res/splash-blank.jpg")
		outputFile = file("$res/$splashImage")
		version = gitLastTag
		//version = gitVersion()
		release = new Date().format('MMMM d, yyyy') 
		licensed = 'To Eng. Sami Alfattani'

		//println "about-"*5
	}
	
	// launch4j: put all configuration of launch4j here 
	createExe {
		dependsOn 'makeAboutImage'

		fileDescription	 = "Electronic Quizes - Client"
		copyright = "All Copyright are recieved (c) 2016~2018, This is a program for e-quizes in order to make exams for students and mark registeration."
		companyName = "Eng. Sami Alfattani"

		outputDir = "." 				//relative to $buildDir default "launch4j"
		
		bundledJrePath = "$jreImageDir" //Relative to exe 		default "image"
		
		//where launc4j puts all jars, this will be DELETED after task
		libraryDir = "launch4j-lib"

		classpath = ["$jreImageDir/lib/*"] 	//Relative to exe 	default []
		jar = "$jreImageDir/lib/${project.tasks['jar'].archiveName}" 
		mainClassName = this.mainClassName
	    
	    //headerType = "console" 			//no splash with console
	    headerType = "gui" 
		//splashFileName = 'splash-client.bmp' //realative to exe
	    icon = "exam.ico" 				//realative to exe
		
		jvmOptions = ["-splash:$splashImage"]

		outfile = 'eQuiz-client.exe'
		productName	= 'eQuiz-client' 
		internalName = 'eQuiz-client-' + gitLastTag /*gitVersion() */
		
		//jreMinVersion = '9.0'
		jdkPreference = 'jdkOnly' //jreOnly|preferJre|preferJdk|jdkOnly

		// initialHeapSize = 0 //Optional
		// maxHeapSize = 4096
		// maxHeapPercent = 50
		errTitle = "Java is not Supported on this Computer"
		downloadUrl = 'https://www.java.com/ES/download/'
		messagesStartupError "An error occurred while starting the application.\n" + "حصلت مشكلة أثناء التشغيل" 
		messagesBundledJreError "This application was configured to use a bundled Java Runtime Environment but the runtime is missing or corrupted.\n" + "من المفترض أن يعمل هذا البرنامج على نسخة خاصة من الجافا متضمنة معه،ويبدو أنه قد حصل خطأ في هذه النسخة"
		messagesJreVersionError "This application requires a Java Runtime Environment\n" + "هذا البرنامج يحتاج إلى الجافا"
		messagesLauncherError "The registry refers to a nonexistent Java Runtime Environment installation or the runtime is corrupted.\n" + "يشير الريجستري إلى عدم تحميل الجافا أو النسخة معطوبة"
		messagesInstanceAlreadyExists "An application instance is already running.\n" + "هذا البرنامج حاليا قيد التشغيل ولا يمكن فتح أكثر من نسخة واحدة"
		//see more: https://github.com/TheBoegl/gradle-launch4j
	}

	createExe.doFirst {
		copy{
			from "$projPath/src/main/resources/images"
			include "exam.ico"
			include splashImage
			into "$buildDir"
		}
	}

	createExe.doLast {
		delete{
			delete "$buildDir/launch4j-lib"
		}		
	}

	apply from: file('../../eqCore/winrar/build.gradle');

	def get_git_message()
	{
		def stdout = new ByteArrayOutputStream()
		//git log -n 1 --format=format:%B
		exec {
			commandLine 'git', 'log', '-n', '1', '--format=format:%B'
			standardOutput = stdout
		}
		return stdout.toString().trim()
	}

	def first_line( x )
	{
			return x.substring(0 , x.indexOf('\n')+1 )	
	}

	makeSFXConfig
	{
		outputDir = file("$buildDir")
		version = gitLastTag /*gitVersion()*/
		release = new Date().format('MMMM d, yyyy') 
		git_message = get_git_message()
		
		git_message_title = first_line(git_message)
		git_message = git_message.split('\n').drop(1).join('\n')

		shortcut.destType = 'desktop'
		shortcut.srcName = 'eQuiz-client.exe -h 10.4.10.100 -p 10000' 	//relative to archive
		shortcut.destFolder = ''
		shortcut.desc = 'dddddd'
		shortcut.shortcutName = 'run-eQuiz'
		shortcut.shortcutIcon = 'exam.ico' 								//relative to archive

		extractLocation = 'program files' //'current' or 'program files'
		extractDir = 'eQuiz'
	}

	makeSFX
	{
		def outputDir =  file("$buildDir")
		def outputFile = file("$outputDir/eQuiz-Installer-with-jre.exe")
		def packageIcon = file("$outputDir/exam.ico")
		def packageImage = file("$outputDir/splash-client.jpg")
		def packgeList =  [ 
		 	'splash-client.jpg' , 
			'exam.ico', 
		 	'eQuiz-client.exe' ,
			'equiz-client'	 
			 ]  

		//workingDir = outputDir
		//commandLine = [ 'cmd', '/c', 'echo', 'ff']
 		
		//'cmd', '/c', 
		def commandLine = [ 'cmd', '/c', 
			'rar', 'a', '-m5',
			"-sfx \"${outputFile.absolutePath}\"" , 
			'-ep1', 
			"-iicon\"${packageIcon.absolutePath}\"" , 
			"-iimage\"${packageImage.absolutePath}\"" , 
			"-z\"${outputDir}\\SFX-package.conf\""
			] + packgeList

		file("$buildDir/sfx-make.bat").write commandLine.join(' ^\n ') + "\npause";
	} 
